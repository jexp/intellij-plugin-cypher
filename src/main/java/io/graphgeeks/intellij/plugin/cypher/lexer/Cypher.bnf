{
  parserClass="io.graphgeeks.intellij.plugin.cypher.parser.CypherParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Cypher"
  psiImplClassSuffix="Impl"
  psiPackage="io.graphgeeks.intellij.plugin.cypher.psi"
  psiImplPackage="io.graphgeeks.intellij.plugin.cypher.psi.impl"

  elementTypeHolderClass="io.graphgeeks.intellij.plugin.cypher.psi.CypherTypes"
  elementTypeClass="io.graphgeeks.intellij.plugin.cypher.psi.CypherElementType"
  tokenTypeClass="io.graphgeeks.intellij.plugin.cypher.psi.CypherTokenType"

  tokens=[
    // keywords
    K_MATCH="regexp:(M|m)(A|a)(T|t)(C|c)(H|h)"
    K_RETURN="regexp:(R|r)(E|e)(T|t)(U|u)(R|r)(N|n)"
    K_RETURN_DISTINCT="regexp:(R|r)(E|e)(T|t)(U|u)(R|r)(N|n) (D|d)(I|i)(S|s)(T|t)(I|i)(N|n)(C|c)(T|t)"
    K_UNION="regexp:(U|u)(N|n)(I|i)(O|o)(N|n)"
    K_UNION_ALL="regexp:(U|u)(N|n)(I|i)(O|o)(N|n) (A|a)(L|l)(L|l)"
    K_LOAD_CSV="regexp:((L|l)(O|o)(A|a)(D|d) (C|c)(S|s)(V|v))"
    K_WITH_HEADERS="regexp:(W|w)(I|i)(T|t)(H|h) (H|h)(E|e)(A|a)(D|d)(E|e)(R|r)(S|s)"
    K_FROM="regexp:(F|f)(R|r)(O|o)(M|m)"
    K_AS="regexp:(A|a)(S|s)"
    K_FIELDTERMINATOR="regexp:(F|f)(I|i)(E|e)(L|l)(D|d)(T|t)(E|e)(R|r)(M|m)(I|i)(N|n)(A|a)(T|t)(O|o)(R|r)"
    K_CREATE="regexp:(C|c)(R|r)(E|e)(A|a)(T|t)(E|e)"
    K_CONSTRAINT_ON="regexp:(C|c)(O|o)(N|n)(S|s)(T|t)(R|r)(A|a)(I|i)(N|n)(T|t) (O|o)(N|n)"
    K_ASSERT="regexp:(A|a)(S|s)(S|s)(E|e)(R|r)(T|t)"
    K_IS_UNIQUE="regexp:(I|i)(S|s) (U|u)(N|n)(I|i)(Q|q)(U|u)(E|e)"
    K_ASSERT_EXISTS="regexp:(A|a)(S|s)(S|s)(E|e)(R|r)(T|t) (E|e)(X|x)(I|i)(S|s)(T|t)(S|s)"
    K_CREATE_INDEX_ON="regexp:(C|c)(R|r)(E|e)(A|a)(T|t)(E|e) (I|i)(N|n)(D|d)(E|e)(X|x) (O|o)(N|n)"
    K_DROP="regexp:(D|d)(R|r)(O|o)(P|p)"
    K_DROP_INDEX_ON="regexp:(D|d)(R|r)(O|o)(P|p) (I|i)(N|n)(D|d)(E|e)(X|x) (O|o)(N|n)"
    K_START="regexp:(S|s)(T|t)(A|a)(R|r)(T|t)"
    K_WHERE="regexp:(W|w)(H|h)(E|e)(R|r)(E|e)"
    K_NODE="regexp:(N|n)(O|o)(D|d)(E|e)"
    K_RELATIONSHIP="regexp:(R|r)(E|e)(L|l)(A|a)(T|t)(I|i)(O|o)(N|n)(S|s)(H|h)(I|i)(P|p)"
    K_REL="regexp:(R|r)(E|e)(L|l)"
    K_OPTIONAL_MATCH="regexp:(O|o)(P|p)(T|t)(I|i)(O|o)(N|n)(A|a)(L|l) (M|m)(A|a)(T|t)(C|c)(H|h)"
    K_MATCH="regexp:(M|m)(A|a)(T|t)(C|c)(H|h)"
    K_USING_INDEX="regexp:(U|u)(S|s)(I|i)(N|n)(G|g) (I|i)(N|n)(D|d)(E|e)(X|x)"
    K_USING_JOIN_ON="regexp:(U|u)(S|s)(I|i)(N|n)(G|g) (J|j)(O|o)(I|i)(N|n) (O|o)(N|n)"
    K_USING_SCAN="regexp:(U|u)(S|s)(I|i)(N|n)(G|g) (S|s)(C|c)(A|a)(N|n)"
    K_SHORTESTPATH="regexp:(S|s)(H|h)(O|o)(R|r)(T|t)(E|e)(S|s)(T|t)(P|p)(A|a)(T|t)(H|h)"
    K_ALLSHORTESTPATHS="regexp:(A|a)(L|l)(L|l)(S|s)(H|h)(O|o)(R|r)(T|t)(E|e)(S|s)(T|t)(P|p)(A|a)(T|t)(H|h)(S|s)"
    K_UNWIND="regexp:(U|u)(N|n)(W|w)(I|i)(N|n)(D|d)"
    K_MERGE="regexp:(M|m)(E|e)(R|r)(G|g)(E|e)"
    K_ON_MATCH="regexp:(O|o)(N|n) (M|m)(A|a)(T|t)(C|c)(H|h)"
    K_ON_CREATE="regexp:(O|o)(N|n) (C|c)(R|r)(E|e)(A|a)(T|t)(E|e)"
    K_SET="regexp:(S|s)(E|e)(T|t)"
    K_CREATE_UNIQUE="regexp:(C|c)(R|r)(E|e)(A|a)(T|t)(E|e) (U|u)(N|n)(I|i)(Q|q)(U|u)(E|e)"
    K_DELETE="regexp:(D|d)(E|e)(L|l)(E|e)(T|t)(E|e)"
    K_DETACH_DELETE="regexp:(D|d)(E|e)(T|t)(A|a)(C|c)(H|h) (D|d)(E|e)(L|l)(E|e)(T|t)(E|e)"
    K_REMOVE="regexp:(R|r)(E|e)(M|m)(O|o)(V|v)(E|e)"
    K_FOREACH="regexp:(F|f)(O|o)(R|r)(E|e)(A|a)(C|c)(H|h)"
    K_IN="regexp:(I|i)(N|n)"
    K_WITH_DISTINCT="regexp:(W|w)(I|i)(T|t)(H|h) (D|d)(I|i)(S|s)(T|t)(I|i)(N|n)(C|c)(T|t)"
    K_WITH="regexp:(W|w)(I|i)(T|t)(H|h)"
    K_ORDER_BY="regexp:(O|o)(R|r)(D|d)(E|e)(R|r) (B|b)(Y|y)"
    K_DESCENDING="regexp:(D|d)(E|e)(S|s)(C|c)(E|e)(N|n)(D|d)(I|i)(N|n)(G|g)"
    K_DESC="regexp:(D|d)(E|e)(S|s)(C|c)"
    K_ASCENDING="regexp:(A|a)(S|s)(C|c)(E|e)(N|n)(D|d)(I|i)(N|n)(G|g)"
    K_ASC="regexp:(A|a)(S|s)(C|c)"
    K_SKIP="regexp:(S|s)(K|k)(I|i)(P|p)"
    K_LIMIT="regexp:(L|l)(I|i)(M|m)(I|i)(T|t)"
    K_USING_PERIODIC_COMMIT="regexp:(U|u)(S|s)(I|i)(N|n)(G|g) (P|p)(E|e)(R|r)(I|i)(O|o)(D|d)(I|i)(C|c) (C|c)(O|o)(M|m)(M|m)(I|i)(T|t)"
    K_XOR="regexp:(X|x)(O|o)(R|r)"
    K_OR="regexp:(O|o)(R|r)"
    K_AND="regexp:(A|a)(N|n)(D|d)"
    K_NOT="regexp:(N|n)(O|o)(T|t)"
    K_STARTS_WITH="regexp:(S|s)(T|t)(A|a)(R|r)(T|t)(S|s) (W|w)(I|i)(T|t)(H|h)"
    K_ENDS_WITH="regexp:(E|e)(N|n)(D|d)(S|s) (W|w)(I|i)(T|t)(H|h)"
    K_CONTAINS="regexp:(C|c)(O|o)(N|n)(T|t)(A|a)(I|i)(N|n)(S|s)"
    K_IS_NULL="regexp:(I|i)(S|s) (N|n)(U|u)(L|l)(L|l)"
    K_IS_NOT_NULL="regexp:(I|i)(S|s) (N|n)(O|o)(T|t) (N|n)(U|u)(L|l)(L|l)"
    K_TRUE="regexp:(T|t)(R|r)(U|u)(E|e)"
    K_FALSE="regexp:(F|f)(A|a)(L|l)(S|s)(E|e)"
    K_NULL="regexp:(N|n)(U|u)(L|l)(L|l)"
    K_COUNT="regexp:(C|c)(O|o)(U|u)(N|n)(T|t)"
    K_FILTER="regexp:(F|f)(I|i)(L|l)(T|t)(E|e)(R|r)"
    K_EXTRACT="regexp:(E|e)(X|x)(T|t)(R|r)(A|a)(C|c)(T|t)"
    K_REDUCE="regexp:(R|r)(E|e)(D|d)(U|u)(C|c)(E|e)"
    K_ALL="regexp:(A|a)(L|l)(L|l)"
    K_ANY="regexp:(A|a)(N|n)(Y|y)"
    K_NONE="regexp:(N|n)(O|o)(N|n)(E|e)"
    K_SINGLE="regexp:(S|s)(I|i)(N|n)(G|g)(L|l)(E|e)"
    K_CASE="regexp:(C|c)(A|a)(S|s)(E|e)"
    K_DISTINCT="regexp:(D|d)(I|i)(S|s)(T|t)(I|i)(N|n)(C|c)(T|t)"
    K_ELSE="regexp:(E|e)(L|l)(S|s)(E|e)"
    K_END="regexp:(E|e)(N|n)(D|d)"
    K_WHEN="regexp:(W|w)(H|h)(E|e)(N|n)"
    K_THEN="regexp:(T|t)(H|h)(E|e)(N|n)"

    // literals
    l_identifier="regexp:[a-zA-Z_$][a-zA-Z_$\d]*"
    l_identifier_text="regexp:\`[^`]+\`"
    l_decimal="regexp:[+-]?(([1-9]\d+)|([0-9]))\.\d+"
    l_integer="regexp:[+-]?(([1-9]\d+)|([0-9]))"
    l_string="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\.)*\")"

    // base
    leftBrace="("
    rightBrace=")"
    leftCurlyBrace="{"
    rightCurlyBrace="}"
    leftSquareBrace="["
    rightSquareBrace="]"
    semicolon=";"
    colon=":"
    dot="."
    equal="="
    lessThen="<"
    greatherThen=">"
    plus="+"
    minus="-"
    mul="*"
    bactick="`"
    comma=","
    questionSign="?"
    pipe="|"
    range=".."
    plusEquals="+="
    invalidNotEquals="<>"
    notEquals="!="
    lessThanEquals="<="
    greaterThanEquals=">="
    divide="/"
    modulo="%"
    pow="^"
    regexMatch="=~"

    startRelPattern="()-["
    startRelPatternDirected="()<-["
    endRelPattern="]-()"
    endRelPatternDirected="]->()"

    // comments
    lineComment="regexp://.*"
    blockComment="regexp:/\*(.|\n)*\*/"
  ]
}

cypher ::= Statements *

private Statements ::= !<<eof>> Statement ';' {pin=1 recoverWhile=statement_recover}
private statement_recover ::= !(';')

/*************************
 * Statement
 */
Statement ::= Command | Query

/*************************
 * Command
 */
Command ::= CreateUniqueConstraint
          | CreateNodePropertyExistenceConstraint
          | CreateRelationshipPropertyExistenceConstraint
          | CreateIndex
          | DropUniqueConstraint
          | DropNodePropertyExistenceConstraint
          | DropRelationshipPropertyExistenceConstraint
          | DropIndex

CreateUniqueConstraint ::= K_CREATE UniqueConstraintSyntax
CreateNodePropertyExistenceConstraint ::= K_CREATE NodePropertyExistenceConstraintSyntax
CreateRelationshipPropertyExistenceConstraint ::= K_CREATE RelationshipPropertyExistenceConstraintSyntax
CreateIndex ::= K_CREATE_INDEX_ON NodeLabel "(" PropertyKeyName ")"
DropUniqueConstraint ::= K_DROP UniqueConstraintSyntax
DropNodePropertyExistenceConstraint ::= K_DROP NodePropertyExistenceConstraintSyntax
DropRelationshipPropertyExistenceConstraint ::= K_DROP RelationshipPropertyExistenceConstraintSyntax
DropIndex ::= K_DROP_INDEX_ON NodeLabel "(" PropertyKeyName ")"

UniqueConstraintSyntax ::= K_CONSTRAINT_ON "(" Identifier NodeLabel ")" K_ASSERT PropertyExpression K_IS_UNIQUE
NodePropertyExistenceConstraintSyntax ::= K_CONSTRAINT_ON "(" Identifier NodeLabel ")" K_ASSERT_EXISTS "(" PropertyExpression ")"
RelationshipPropertyExistenceConstraintSyntax ::= K_CONSTRAINT_ON RelationshipPatternSyntax K_ASSERT_EXISTS "(" PropertyExpression ")"
RelationshipPatternSyntax ::= ("()-[" Identifier RelType "]-()") // todo: move this to literals
      | ("()-[" Identifier RelType "]->()")
      | ("()<-[" Identifier RelType "]-()")

/*************************
 * Query
 */
Query ::= RegularQuery | BulkImportQuery // empty input is OK?

//-- RegularQuery
RegularQuery ::= SingleQuery Union*
Union ::= (K_UNION_ALL | K_UNION) SingleQuery
SingleQuery ::= Clause+
Clause ::= LoadCSV
    | Start
    | Match
    | Unwind
    | Merge
    | Create
    | SetClause
    | Delete
    | Remove
    | Foreach
    | With
    | Return
    //| Pragma // todo: what the hell is this?

//-- BulkImportQuery
BulkImportQuery ::= PeriodicCommitHint LoadCSVQuery
PeriodicCommitHint ::= K_USING_PERIODIC_COMMIT SignedIntegerLiteral?
LoadCSVQuery ::= LoadCSV Clause*

/*************************
 * Clauses
 */
//-- Load csv
LoadCSV ::= K_LOAD_CSV K_WITH_HEADERS? K_FROM Expression K_AS Identifier (K_FIELDTERMINATOR StringLiteral)?

//-- Start
Start ::= K_START (StartPoint ("," StartPoint)*)? Where?
StartPoint ::= Identifier "=" Lookup
Lookup ::= NodeLookup | RelationshipLookup

NodeLookup ::= K_NODE (NodeIndexLookup | NodeIndexQuery | NodeIdLookup)
NodeIndexLookup ::= IdentifiedIndexLookup
NodeIndexQuery ::= IndexQuery
NodeIdLookup ::= "(" (LiteralIds | Parameter | "*") ")"

RelationshipLookup ::= (K_RELATIONSHIP | K_REL) (RelationshipIndexLookup | RelationshipIndexQuery | RelationshipIdLookup)
RelationshipIndexLookup ::= IdentifiedIndexLookup
RelationshipIndexQuery ::= IndexQuery
RelationshipIdLookup ::= "(" (LiteralIds | Parameter | "*") ")"

IdentifiedIndexLookup ::= ":" SymbolicNameString "(" SymbolicNameString "=" (StringLiteral | Parameter) ")"
IndexQuery ::= ":" SymbolicNameString "(" (StringLiteral | Parameter) ")"
LiteralIds ::= UnsignedIntegerLiteral ("," UnsignedIntegerLiteral)*

//-- Match
Match ::= (K_OPTIONAL_MATCH | K_MATCH) Pattern Hint* Where?
Hint ::= (K_USING_INDEX Identifier NodeLabel "(" PropertyKeyName ")")
       | (K_USING_JOIN_ON Identifier ("," Identifier)*)
       | (K_USING_SCAN Identifier NodeLabel)

//-- Merge
Merge ::= K_MERGE PatternPart MergeAction*
MergeAction ::= (K_ON_MATCH SetClause)
              | (K_ON_CREATE SetClause)

//-- SetClause
SetClause ::= K_SET SetItem ("," SetItem)*
SetItem ::= (PropertyExpression "=" Expression)
          | (Identifier "=" Expression)
          | (Identifier "+=" Expression)
          | (Identifier NodeLabels)

//-- Remove
Remove ::= K_REMOVE RemoveItem ("," RemoveItem)*
RemoveItem ::= (Identifier NodeLabels) | PropertyExpression

//-- Return
With ::= (K_WITH_DISTINCT ReturnBody Where?)
       | (K_WITH ReturnBody Where?)
Return ::= (K_RETURN_DISTINCT ReturnBody)
         | (K_RETURN ReturnBody)
ReturnBody ::= ReturnItems Order? Skip? Limit?
ReturnItems ::= ("*" ReturnItem? ("," ReturnItem)*)
              | (ReturnItem ("," ReturnItem)*)
ReturnItem ::= (Expression K_AS Identifier) | (Expression)
Order ::= K_ORDER_BY SortItem ("," SortItem)*
SortItem ::= (Expression (K_DESCENDING | K_DESC))
           | (Expression (K_ASCENDING | K_ASC)?)
Skip ::= K_SKIP Expression
Limit ::= K_LIMIT Expression

//-- Other
Create ::= (K_CREATE_UNIQUE Pattern)
         | (K_CREATE Pattern)
Delete ::= (K_DELETE Expression ("," Expression)*)
         | (K_DETACH_DELETE Expression ("," Expression)*)
Unwind ::= K_UNWIND Expression K_AS Identifier
Foreach ::= K_FOREACH "(" Identifier K_IN Expression "|" Clause+ ")"
Where ::= K_WHERE Expression

/*************************
 * Patterns
 */
Pattern ::= PatternPart ("," PatternPart)*
PatternPart ::= (Identifier "=" AnonymousPatternPart) | AnonymousPatternPart
AnonymousPatternPart ::= ShortestPathPattern | PatternElement
ShortestPathPattern ::= (K_SHORTESTPATH "(" PatternElement ")")
                      | (K_ALLSHORTESTPATHS "(" PatternElement ")")
PatternElement ::= (NodePattern PatternElementChain*)
                 | ("(" PatternElement ")") // Probably it's OK that there is "empty match" case.
PatternElementChain ::= RelationshipPattern NodePattern

RelationshipPattern ::= (LeftArrowHead Dash RelationshipDetail? Dash RightArrowHead)
                      | (LeftArrowHead Dash RelationshipDetail? Dash)
                      | (Dash RelationshipDetail? Dash RightArrowHead)
                      | (Dash RelationshipDetail? Dash)

RelationshipDetail ::= "[" MaybeIdentifier "?"? RelationshipTypes MaybeVariableLength MaybeProperties "]"
RelationshipTypes ::= (":" RelTypeName ("|" ":"? RelTypeName)*)?
MaybeVariableLength ::= ("*" RangeLiteral?)?

NodePattern ::= ("(" MaybeIdentifier MaybeNodeLabels MaybeProperties ")")
              | (MaybeIdentifier MaybeNodeLabels MaybeProperties)
MaybeNodeLabels ::= NodeLabels?

MaybeIdentifier ::= Identifier?
MaybeProperties ::= (MapLiteral | Parameter)?

RelationshipsPattern ::= NodePattern PatternElementChain+

/*************************
 * Expression
 */
Expression ::= Expression12
private Expression12 ::= Expression11 (K_OR Expression11)*
private Expression11 ::= Expression10 (K_XOR Expression10)*
private Expression10 ::= Expression9 (K_AND Expression9)*
private Expression9 ::= (K_NOT Expression9) | Expression8
private Expression8 ::= Expression7 PartialComparisonExpression*
private PartialComparisonExpression ::= ("=" Expression7)
                                      | ("<>" Expression7)
                                      | ("!=" Expression7)
                                      | ("<" Expression7)
                                      | (">" Expression7)
                                      | ("<=" Expression7)
                                      | (">=" Expression7)
private Expression7 ::= Expression6 (("-" Expression6) | ("+" Expression6))*
private Expression6 ::= Expression5 (("*" Expression5) | ("/" Expression5) | ("%" Expression5))*
private Expression5 ::= Expression4 ("^" Expression4)*
private Expression4 ::= Expression3 | ("-" Expression4) | ("+" Expression4)
private Expression3 ::= Expression2 (
    ("[" Expression "]")
  | ("[" Expression? ".." Expression? "]")
  | ("=~" Expression2)
  | (K_IN Expression2)
  | (K_STARTS_WITH  Expression2)
  | (K_ENDS_WITH  Expression2)
  | (K_CONTAINS  Expression2)
  | (K_IS_NULL  Expression2)
  | (K_IS_NOT_NULL  Expression2)
)*
private Expression2 ::= Expression1 (PropertyLookup | NodeLabels)*
private Expression1 ::= NumberLiteral
              | StringLiteral
              | Parameter
              | (K_TRUE)
              | (K_FALSE)
              | (K_NULL)
              | CaseExpression
              | (K_COUNT "(" "*" ")")
              | MapLiteral
              | ListComprehension
              | ("[" Expression? ("," Expression)* "]")
              | (K_FILTER "(" FilterExpression ")")
              | (K_EXTRACT "(" FilterExpression ("|" Expression)? ")")
              | (K_REDUCE "(" Identifier "=" Expression "," IdInColl "|" Expression ")")
              | (K_ALL "(" FilterExpression ")")
              | (K_ANY "(" FilterExpression ")")
              | (K_NONE "(" FilterExpression ")")
              | (K_SINGLE "(" FilterExpression ")")
              | ShortestPathPattern
              | RelationshipsPattern
              | parenthesizedExpression
              | FunctionInvocation
              | Identifier

PropertyLookup ::= "." PropertyKeyName // todo: finish this rule, legacy part is missing
ListComprehension ::= "[" FilterExpression ("|"  Expression)? "]"
FilterExpression ::= IdInColl (K_WHERE)? Expression
IdInColl ::= Identifier K_IN Expression
parenthesizedExpression ::= "(" Expression ")"
FunctionInvocation ::= FunctionName "(" K_DISTINCT? Expression? ("," Expression)* ")"
CaseExpression ::= ((K_CASE CaseAlternatives+) | (K_CASE Expression CaseAlternatives+))
                   (K_ELSE Expression)?
                   K_END
CaseAlternatives ::= K_WHEN Expression K_THEN Expression
PropertyExpression ::= Expression1 (PropertyLookup)+

/*************************
 * Literals
 */
Identifier ::= SymbolicNameString
FunctionName ::= SymbolicNameString
Parameter ::= "{" (SymbolicNameString | UnsignedDecimalInteger) "}"
NodeLabel ::= ":" LabelName
StringLiteral ::= l_string
LabelName ::= SymbolicNameString
RelType ::= ":" RelTypeName
RelTypeName ::= SymbolicNameString
PropertyKeyName ::= SymbolicNameString
RangeLiteral ::= (UnsignedIntegerLiteral? ".." UnsignedIntegerLiteral?) | (UnsignedIntegerLiteral)
MapLiteral ::= "{" (PropertyKeyName ":" Expression)? ("," PropertyKeyName ":" Expression)* "}"
NodeLabels ::= NodeLabel+
//SignedIntegerLiteral ::= HexInteger | OctalInteger | DecimalInteger // todo: this is real implementation
SignedIntegerLiteral ::= SignedDecimalInteger
UnsignedIntegerLiteral ::= UnsignedDecimalInteger
NumberLiteral ::= DoubleLiteral | SignedIntegerLiteral
DoubleLiteral ::= l_decimal // todo: make real double here

/*************************
 * Base
 */
private SymbolicNameString ::=  UnescapedSymbolicNameString | EscapedSymbolicNameString
private EscapedSymbolicNameString ::= l_identifier_text
private UnescapedSymbolicNameString ::= l_identifier
private UnsignedDecimalInteger ::= l_integer
private SignedDecimalInteger ::= l_integer
private Dash ::= "-" // todo: migrate all variants
private LeftArrowHead ::= "<" // todo: migrate all variants
private RightArrowHead ::= ">" // todo: migrate all variants
